apply plugin: 'idea'
apply plugin: 'groovy'
sourceSets {
	integrationTest {
		groovy {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integrationTest/groovy')
		}
		resources.srcDir file('src/integrationTest/resources')
	}
}
configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}
task integrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	shouldRunAfter test
	outputs.upToDateWhen { false }
}
check.dependsOn integrationTest

// Allow running integration tests in IntelliJ
idea {
	module {
		sourceSets.integrationTest.allSource.srcDirs.each { srcDir -> module.testSourceDirs += srcDir }
	}
}

dependencies {
	integrationTestCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.6', ext: 'pom'
	integrationTestCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
}
